apply plugin: "maven-publish"
apply plugin: "signing"

project.group = rootProject.group
project.version = rootProject.version
project.description = "This is a Gradle plugin for Android applications which resolves XML string references in other XML strings."
ext {
    publicName = "Android String XML Reference"
    url = "https://github.com/LikeTheSalad/android-string-reference"
    vcsUrl = "https://github.com/LikeTheSalad/android-string-reference.git"
    issueTrackerUrl = "https://github.com/LikeTheSalad/android-string-reference/issues"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

task javadocJar(type: Jar) {
    from javadoc.destinationDir
    classifier 'javadoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            version = project.version

            artifact sourcesJar
            artifact javadocJar
            from components.java

            pom {
                name = project.publicName
                description = project.description
                url = project.url
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'LikeTheSalad'
                        name = 'Cesar Munoz'
                        email = 'likethesalad@gmail.com'
                    }
                }
                scm {
                    url = project.url
                    connection = project.vcsUrl
                }
                issueManagement {
                    url = project.issueTrackerUrl
                }
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}