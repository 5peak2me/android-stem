plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm'
    id 'kotlin-kapt'
    id 'com.gradle.plugin-publish' version '0.18.0'
}

dependencies {
    api gradleApi()
    implementation project(':common-tools')
    implementation project(':templates-provider-plugin')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation 'io.github.classgraph:classgraph:4.8.138'
    compileOnly "com.android.tools.build:gradle:$androidBuild_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    testImplementation "com.likethesalad.tools.testing:android-gradle:$testingUtilities_version"
    testImplementation "com.likethesalad.tools.testing:unit-testing:$testingUtilities_version"
    testImplementation "com.android.tools.build:gradle:$androidBuild_version"
    testImplementation "net.lingala.zip4j:zip4j:2.9.1"
}

tasks.withType(PluginUnderTestMetadata).configureEach {
    pluginClasspath.from(configurations.compileOnly)
}

kapt {
    correctErrorTypes = true
}

sourceSets {
    functionalTest {
        java {
            srcDir file('src/functionalTest/java')
        }
        resources {
            srcDir file('src/functionalTest/resources')
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

pluginBundle {
    website = rootProject.artifactPublisher.url.get()
    vcsUrl = rootProject.artifactPublisher.vcsUrl.get()
    description = rootProject.description
    tags = ['android', 'resolved-strings', 'xml-strings']
}

gradlePlugin {
    plugins {
        placeholderResolverPlugin {
            id = 'placeholder-resolver'
            displayName = rootProject.artifactPublisher.displayName.get()
            implementationClass = 'com.likethesalad.placeholder.ResolvePlaceholdersPlugin'
        }
        templatesProvider {
            id = 'resource.templates.provider'
            implementationClass = 'com.likethesalad.android.templates.provider.TemplatesProviderPlugin'
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}